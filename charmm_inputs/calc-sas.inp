*########################################################
* Written by Vincent Zoete
* Modified by Michel A. Cuendet
* For questions, please contact vincent.zoete _at_ unil.ch
* or michel.cuendet _at_ unil.ch
*########################################################
*

STREAM ./data/loader.str

STREAM ./data/definitions.str

!================================================================
! Treat trajectory
!================================================================

!--- Open output files for writing
OPEN UNIT 13 CARD WRITE NAME total/buried-sasa.dat

NBOND @nbstr


SET n 0
LABEL loop

  INCR n BY 1
  
  COOR INIT SELE ALL END

  OPEN UNIT 2 READ CARD NAME frames-comp/@n-in.crd
  READ COOR CARD RESI UNIT 2 
  ! ###### Here we should use RESI if atoms have been deleted
  ! ###### But we can't if atom order is not preserved in trajectory CRD files,
  ! ###### as is the case when using trajectories from NAMD. 
  CLOSE UNIT 2

 !--- Calculates SAS

 COOR SURF ACCE RPROBE 1.4 SELE A .OR. B END
! #### There might be other things than A and B in complex : ignore it!

 SET c ?AREA
 OPEN UNIT 1 WRITE CARD NAME frames-comp/@n-sas.crd
 WRITE COOR CARD UNIT 1
 CLOSE UNIT 1
 
 COOR SURF ACCE RPROBE 1.4 SELE A END
 SET a ?AREA
 OPEN UNIT 1 WRITE CARD NAME frames-a/@n-sas.crd
 WRITE COOR CARD UNIT 1
 CLOSE UNIT 1

 COOR SURF ACCE RPROBE 1.4 SELE B END
 SET b ?AREA
 OPEN UNIT 1 WRITE CARD NAME frames-b/@n-sas.crd
 WRITE COOR CARD UNIT 1
 CLOSE UNIT 1

 CALC bu = @a + @b - @c

 WRITE TITLE UNIT 13
 * @n  @a  @b  @c : @bu
 *

IF @n .LT. @nframes GOTO loop


CLOSE UNIT 12
CLOSE UNIT 13



!================================================================
!	END
!================================================================

STOP

