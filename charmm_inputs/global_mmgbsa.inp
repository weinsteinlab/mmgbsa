*########################################################
* Written by Vincent Zoete
* Modified by Michel A. Cuendet
* For questions, please contact vincent.zoete _at_ unil.ch
* or michel.cuendet _at_ unil.ch
*########################################################
*

STREAM ./loader.str

STREAM definitions.str


!================================================================
! Treat trajectory
!================================================================

!--- Open output files for writing

NBOND @nbstr

OPEN UNIT 20 CARD WRITE NAME global_mmgbsa.dat

SET n 0
LABEL loop

  INCR n BY 1
  
  COOR INIT SELE ALL END

  OPEN UNIT 2 READ CARD NAME frames-comp/@n-alpha.crd
  READ COOR CARD RESI UNIT 2 
  CLOSE UNIT 2
  

 !--- Calculates complex solvation energy with GBMV model II

 GBMV BETA -20 EPSILON 80 DN 1.0 watr 1.4 GEOM - KAPPA @kappa
     TOL 1e-8 BUFR 0.5 Mem 10 CUTA 20 HSX1 -0.125 HSX2 0.25 -
     ALFRQ 1 EMP 1.5 P4 0.0 P6 8.0 P3 0.70 ONX 1.9 OFFX 2.1 -
     WTYP 2 NPHI 38 SHIFT -0.102 SLOPE 0.9085 CORR 1 SA 0.0

 ENERGY @nbstr

 SET mysolv ?GBEN
 SET myelec ?ELEC
 SET myvdw  ?VDW
 SET mybond ?BOND
 SET myangle ?ANGL
 SET myurey ?UREY
 SET mydihe ?DIHE
 SET myimpr ?IMPR
 SET myener ?ENER
 
 CALC mybonded = @mybond + @myangle + @myurey + @mydihe + @myimpr
 
 COOR SURF ACCE RPROBE 1.4 SELE ALL END
 SET myarea ?AREA
 
 CALC mysa = @myarea * -0.0072
 
 CALC tot = @mysa + @myener
 

 WRITE TITLE UNIT 20 
 * @n @mybonded @myvdw @myelec @mysolv @myener @myarea @mysa @tot


 GBMV CLEAR


IF @n .LT. @nframes GOTO loop


CLOSE UNIT 12
CLOSE UNIT 13



!================================================================
!	END
!================================================================

STOP

